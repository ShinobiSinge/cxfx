package app

import "args"
import "cx"
import "glfw"
import "gfx"
import "os"
import "mat"
import "v2"
import "v4"

// TODO : handle position in options
// TODO : handle fullscreen in options

// Globals ...
var g_application Application

// Constants ...
var APP_HINT_NONE i32       = 0
var APP_HINT_DEBUG i32      = 1
var APP_HINT_FULLSCREEN i32 = 2
var APP_HINT_RESIZABLE i32  = 4

// Options ...
type Options struct {
	width i32
	height i32
	fps i32
    glVersion str
	hints i32
	dataDir str
}

// Application ...
type Application struct {
	options Options
	exit bool
	name str
	window str
	major i32
	minor i32
    glVersion i32
    x i32
	y i32
	width i32
	height i32
	xscale f32
	yscale f32
	xy_scale mat.v2
	xyxy_scale mat.v4
	fullscreen bool
}

// Help ...
func Help(message str, exitCode i32) {
	printf(message)
	printf("\nUsage:\n")
	printf("++help      : Prints this message.\n")
	printf("++width     : Width in pixels\n")
	printf("++height    : Height in pixels\n")
	printf("++fps       : Target framerate, vsyng disabled when >= 0\n")
	printf("++hints     : Window hint flags (fullscreen, resizable)\n")
	printf("++data      : Resource path\n")
	printf("++glVersion : Select opengl version\n")
    os.Exit(exitCode)
}

// Width ...
func Width()(out i32) {
	out = g_application.width
}

// Height ...
func Height()(out i32) {
	out = g_application.height
}

// ScaleX ...
func ScaleX()(out f32) {
	out = g_application.xscale
}

// ScaleY ...
func ScaleY()(out f32) {
	out = g_application.yscale
}

// ScaleXY ...
func ScaleXY()(out v2) {
	out = g_application.xy_scale
}

// ScaleXYXY ...
func ScaleXYXY()(out v4) {
	out = g_application.xyxy_scale
}

// DataDir ...
func DataDir()(out str) {
	out = g_application.options.dataDir
}

// Fps ...
func Fps()(out i32) {
	out = g_application.options.fps
}

// GLVersion ...
func GLVersion()(out i32) {
    out = g_application.glVersion
}

// Name ...
func Name()(out str) {
	out = g_application.name
}

// WindowName ...
func WindowName()(out str) {
	out = g_application.window
}

// Parse ...
func Parse() {
	var argc i32 = len(os.Args) // ISSUE : doubious compilation error : function 'len' expected receiving variable of type 'i32'; 'str' was provided

	// >> tmp issue #214
	var width i32
	var height i32
	var fps i32
	var hints i32
	var dataDir str
	var help bool
	var glVersion str
    // << tmp issue #214

	var helpMatch bool
	var widthMatch bool
	var heightMatch bool
	var fpsMatch bool
	var hintsMatch bool
	var dataDirMatch bool
    var glVersionMatch bool

	var hintNames []str
	hintNames = []str {"fullscreen", "resizable" }
	var hintValues []i32
	hintValues = []i32 {APP_HINT_FULLSCREEN, APP_HINT_RESIZABLE}

	for a := 0; a < argc; a++ {
		arg := os.Args[a]

		if args.Bool(arg, "help", &help, &helpMatch) {
			Help("", 0)
		}

		if args.I32(arg, "width", &width, &widthMatch) {
			if (width < 0 || width > 65536) {
				 Help(sprintf("invalid value %s\n", arg), cx.PANIC)
			}
			g_application.options.width = width
			continue
		}

		if args.I32(arg, "height", &height, &heightMatch) {
			if (height < 0 || height > 65536) {
				 Help(sprintf("invalid value %s\n", arg), cx.PANIC)
			}
			g_application.options.height = height
			continue
		}

		if args.I32(arg, "fps", &fps, &fpsMatch) {
			if (fps < 0 || fps > 65536) {
				 Help(sprintf("invalid value %s\n", arg), cx.PANIC)
			}
			g_application.options.fps = fps
			continue
		}

		if args.Str(arg, "data", &dataDir, &dataDirMatch) {
			g_application.options.dataDir = dataDir
			continue
		}


        if args.Str(arg, "glVersion", &glVersion, &glVersionMatch) {
            g_application.options.glVersion = glVersion
		    if gfx.GetGLVersionFromString(glVersion) == gfx.GL_VERSION_NONE {
                Help(sprintf("invalid value %s\n", arg), cx.PANIC)
            }
            continue
        }

		if args.Flags(arg, "hints", &hints, &hintsMatch, hintNames, hintValues) {
			continue
		}

		Help(sprintf("invalid argument %s", arg), cx.ASSERT)
	}
}


// Init ...
func Init(name str, window str) (success bool) {
	g_application.exit = false
	g_application.window = window
	g_application.name = name

	Parse()

    g_application.glVersion = gfx.GetGLVersionFromString(g_application.options.glVersion)
    printf("OpenGL Version :  %d, '%s'\n", g_application.glVersion, g_application.options.glVersion)

    if g_application.glVersion == gfx.GL_VERSION_DS_3_2 {
        g_application.major = 3
        g_application.minor = 2
    } else if g_application.glVersion == gfx.GL_VERSION_ES_3_1 {
        g_application.major = 3
        g_application.minor = 1
    } else {
        success = false
        return
    }

	printf("starting %s...\n", g_application.name)

	glfw.Init() // ##0 terminate

    printf("GLFW DONE\n")
	var resizable i32 = glfw.False
	if (g_application.options.hints & APP_HINT_RESIZABLE) == APP_HINT_RESIZABLE {
		resizable = glfw.True
	}

	glfw.WindowHint(glfw.Resizable, resizable)
	glfw.WindowHint(glfw.OpenGLForwardCompat, 1)
	glfw.WindowHint(glfw.OpenGLProfile, glfw.OpenGLCoreProfile)
	glfw.WindowHint(glfw.ContextVersionMajor, g_application.major)
	glfw.WindowHint(glfw.ContextVersionMinor, g_application.minor)
	//glfw.WindowHint(glfw.CocoaRetinaFramebuffer, glfw.True)
	//glfw.WindowHint(glfw.ScaleToMonitor, glfw.True)

	var xscale f32
	var yscale f32
	xscale, yscale = glfw.GetMonitorContentScale()

	g_application.xscale = xscale
	g_application.yscale = yscale
	g_application.xy_scale = v2.make(xscale, yscale)
	g_application.xyxy_scale = v4.make(xscale, yscale, xscale, yscale)

	var windowWidth i32 = f32.i32(i32.f32(g_application.options.width) / xscale)
	var windowHeight i32 = f32.i32(i32.f32(g_application.options.height) / yscale)

	glfw.CreateWindow(g_application.window, windowWidth, windowHeight, g_application.name)

	glfw.MakeContextCurrent(g_application.window)
	glfw.SetWindowPosCallback(g_application.window, "windowPosCallback", "app")
	glfw.SetWindowSizeCallback(g_application.window, "windowSizeCallback", "app")

	g_application.x, g_application.y = glfw.GetWindowPos(g_application.window)
	g_application.width, g_application.height = glfw.GetFramebufferSize(g_application.window)
    //g_application.width = g_application.width / 2
    //g_application.height = g_application.height / 2
    //g_application.width = g_application.options.width
    //g_application.height = g_application.options.height

	var swapInterval i32 = 0
	if g_application.options.fps > 0 {
		swapInterval = 1 // should be based on targetFps
	}

	glfw.SwapInterval(swapInterval)

	if (g_application.options.hints & APP_HINT_FULLSCREEN) == APP_HINT_FULLSCREEN {
		ToggleFullscreen()
	}

    success = true
}

// Exit ...
func Exit() {
	printf("exiting %s...\n", g_application.name)
	g_application.exit = true
}

// ToggleFullscreen ...
func ToggleFullscreen() {
	var fullscreen bool = g_application.fullscreen == false
	if fullscreen {
		g_application.fullscreen = fullscreen
	}
	glfw.Fullscreen(g_application.window, fullscreen, g_application.x, g_application.y, g_application.options.width, g_application.options.height)
	g_application.fullscreen = fullscreen
}

// Running ...
func Running()(out bool) { // ISSUE : member function does not contain expressions
	out = (glfw.ShouldClose(g_application.window) == false && g_application.exit == false)
}

// Resize ...
func Resize(width i32, height i32) {
	/*if g_application.fullscreen == false {
		g_application.options.width = width
		g_application.options.height = height
	}*/

	g_application.width = width
	g_application.height = height
}

// Destroy ...
func Destroy()() {
	glfw.MakeContextCurrent(g_application.window)
}

// BeginFrame ...
func BeginFrame() {
	glfw.MakeContextCurrent(g_application.window)
}

// EndFrame ...
func EndFrame() {
	glfw.PollEvents()
	glfw.SwapBuffers(g_application.window)
}

// SetKeyboardCallback ...
func SetKeyboardCallback(packageName str, functionName str) {
	glfw.SetKeyCallbackEx(g_application.window, functionName, packageName)
}

// SetMouseButtonCallback ...
func SetMouseButtonCallback(packageName str, functionName str) {
	glfw.SetMouseButtonCallbackEx(g_application.window, functionName, packageName)
}

// SetMousePositionCallback ...
func SetMousePositionCallback(packageName str, functionName str) {
	glfw.SetCursorPosCallbackEx(g_application.window, functionName, packageName)
}

// SetWindowResizeCallback ...
func SetWindowResizeCallback(packageName str, functionName str) {
	glfw.SetWindowSizeCallback(g_application.window, functionName, packageName)
}

// SetFramebufferResizeCallback ...
func SetFramebufferResizeCallback(packageName str, functionName str) {
	glfw.SetFramebufferSizeCallback(g_application.window, functionName, packageName)
}

func windowPosCallback(window str, x i32, y i32) {
	if g_application.fullscreen == false {
		g_application.x = x
		g_application.y = y
	}
}

func windowSizeCallback(window str, w i32, h i32) {
	if g_application.fullscreen == false {
		g_application.options.width = w
		g_application.options.height = h
	}
}

